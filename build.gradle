plugins {
    id 'java'
    id 'war'
    id "org.akhikhl.gretty" version "1.2.4"
    id "com.moowork.node" version "0.13"

    id 'com.gradle.build-scan' version '1.0'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

group 'com.epam.memorina'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def pathToWebResources = "${project.projectDir}/src/main/webapp/static/"

node {
    // Version of node to use.
    version = '4.4.7'

    // Version of npm to use.
    npmVersion = '2.15.8'

    npmCommand = 'npm'
    // Base URL for fetching node distributions.
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    //TODO поменять перед deploy
    download = false

    // Set the work directory for unpacking node
    workDir = file("$buildDir/nodejs")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file(pathToWebResources)
}

repositories {
    mavenCentral()
    jcenter()
}

task createBuiltFolder {
    new File(pathToWebResources + "built").mkdir()
    new File(pathToWebResources + "built/js").mkdir()
    new File(pathToWebResources + "built/css").mkdir()
}

task cleanAll(dependsOn: clean, type: Delete) {
    delete pathToWebResources + 'node_modules',
            pathToWebResources + 'built',
            pathToWebResources + 'etc',
            'out'
}

task npmInstall(dependsOn: [createBuiltFolder, npmSetup], type: NpmTask, overwrite: true) {
    args = ['install', '--prefix', pathToWebResources]
}

task npmRunBuild(dependsOn: 'npmInstall', type: NpmTask) {
    args = ['run', 'build', '--prefix', pathToWebResources]
}

build.dependsOn npmRunBuild

gretty {
    port = 8080
    contextPath = '/memorina'
    webappCopy { CopySpec copySpec ->
        exclude '**/node_modules/**', '**/etc/', 'static/js/', '**/package.json'
    }
}

dependencies {
    compile group: 'org.springframework', name: 'spring-context', version: '4.3.1.RELEASE'
    compile group: 'org.springframework', name: 'spring-webmvc', version: '4.3.1.RELEASE'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


